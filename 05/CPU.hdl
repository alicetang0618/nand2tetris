// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:

    // decide the input for A
    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=aIn);
    // decide whether to write to A
    Not(in=instruction[15], out=aInstr);
    Or(a=aInstr, b=instruction[5], out=writeA);
    // load A register if needed
    ARegister(in=aIn, load=writeA, out=aOut, out[0..14]=addressM);

    // choose between A and M for ALU input
    Mux16(a=aOut, b=inM, sel=instruction[12], out=amOut);

    // decide whether to write to D
    And(a=instruction[15], b=instruction[4], out=writeD);
    // load D register if needed
    DRegister(in=aluOut, load=writeD, out=dOut);

    // decide whether to write to M
    And(a=instruction[15], b=instruction[3], out=writeM);

    // implement the ALU chip
    ALU(x=dOut, y=amOut, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=zr, ng=ng);

    // decide whether to jump
    And(a=instruction[2], b=ng, out=jumplt);
    And(a=instruction[1], b=zr, out=jumpeq);
    Or(a=ng, b=zr, out=ngorzr);
    Not(in=ngorzr, out=positive);
    And(a=instruction[0], b=positive, out=jumpgt);

    Or(a=jumplt, b=jumpeq, out=jump1);
    Or(a=jump1, b=jumpgt, out=jump2);
    And(a=instruction[15], b=jump2, out=jump);

    // when there is no jump, increase the pc
    Not(in=jump, out=nojump);
    // implement the program counter
    PC(in=aOut, load=jump, inc=nojump, reset=reset, out[0..14]=pc);
}
// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // increment the output
    Inc16(in=outt, out=add1);

    // determine the 16-bits being fed to the Register
    // if (inc[t] == 1)   out[t+1] = out[t] + 1
	// else               out[t+1] = out[t]  
    Mux16(a=outt, b=add1, sel=inc, out=out1);

	// if (load[t] == 1)  out[t+1] = in[t]
	Mux16(a=out1, b=in, sel=load, out=out2);

	// if      (reset[t] == 1) out[t+1] = 0
	Mux16(a=out2, b=false, sel=reset, out=out3);
    
    // load out3 to the Register
    Register(in=out3, load=true, out=out, out=outt);
}
